program robotmultifuncional

symbol dir1 = PORTA.2
symbol ndir1 = PORTA.4

symbol dir2 = PORTA.3
symbol ndir2 = PORTA.5

symbol ldr1 = PORTB.5
symbol ldr2 = PORTB.4

symbol linea0 = PORTC.5
symbol linea1 = PORTC.6
symbol linea2 = PORTC.7
symbol linea3 = PORTC.4

symbol eco1 = PORTB.0
symbol disparo1 = PORTB.1
symbol eco2 = PORTB.2
symbol disparo2 = PORTB.3

dim temp_ldr1, adc_ldr1, temp_ldr2, adc_ldr2 as word
dim srf05_1, temp_srf05_1, srf05_2, temp_srf05_2 as word
dim banda, sensores as byte
dim velocidad as byte
dim i, modo, old_state as byte

sub procedure set_motor(dim motor1, motor2 as integer)
     if motor1 < 0 then
          motor1 = -motor1
          dir1 = 0
          ndir1 = 1
     else
          dir1 = 1
          ndir1 = 0
     end if

     if motor2 < 0 then
          motor2 = -motor2
          dir2 = 0
          ndir2 = 1
     else
          dir2 = 1
          ndir2 = 0
     end if
     
     if motor1 > 0xFF then
          motor1 = 0xFF
     end if
     
     if motor2 > 0xFF then
          motor2 = 0xFF
     end if
     
     PWM1_Set_Duty(motor1)
     PWM2_Set_Duty(motor2)
end sub

sub procedure seguidor_luz(dim speed as byte)
     temp_ldr1 = Adc_Read(13) ' right
     temp_ldr2 = Adc_Read(11) ' left

     if (adc_ldr1 > temp_ldr1) and (adc_ldr2 > temp_ldr2) then
          set_motor(speed, speed)
     else
          if adc_ldr1 > temp_ldr1 then
               set_motor(speed div 2, speed)
          else
               if adc_ldr2 > temp_ldr2 then
                    set_motor(speed, speed div 2)
               else
                    set_motor(0x00, 0x00)
               end if
          end if
     end if
end sub

sub function ultrasonico1( ) as word
     dim flanco as byte
     dim distan, tiempo as word

     distan = 0x0000
     tiempo = 0x0000
     flanco = 0x00

     disparo1 = 1
     Delay_us(50)
     disparo1 = 0

     do
          if eco1 = 1 then
               TMR0 = 0x00
               flanco = 0xFF
          end if
     loop until( flanco = 0xFF )

     do
          if eco1 = 0 then
               tiempo = TMR0
               flanco = 0x00
          end if
          if TMR0 > 0xFE then
               flanco = 0x00
          end if
     loop until( flanco = 0x00 )
     
     distan = tiempo * 128
     distan = distan / 58

     Delay_ms(50)
     result = distan
end sub

sub function ultrasonico2( ) as word ' left
     dim flanco as byte
     dim distan, tiempo as word

     distan = 0x0000
     tiempo = 0x0000
     flanco = 0x00

     disparo2 = 1
     Delay_us(50)
     disparo2 = 0

     do
          if eco2 = 1 then
               TMR0 = 0x00
               flanco = 0xFF
          end if
     loop until( flanco = 0xFF )

     do
          if eco2 = 0 then
               tiempo = TMR0
               flanco = 0x00
          end if
          if TMR0 > 0xFE then
               flanco = 0x00
          end if
     loop until( flanco = 0x00 )

     distan = tiempo * 128
     distan = distan / 58

     Delay_ms(50)
     result = distan
end sub

sub procedure seguidor_ultrasonico(dim distance, speed as byte)

     srf05_1 = distance
     srf05_2 = distance
     
     temp_srf05_1 = ultrasonico1()
     temp_srf05_2 = ultrasonico2()
     
     if (srf05_1 > temp_srf05_1) and (srf05_2 > temp_srf05_2) then
          set_motor(speed, speed)
     else
          if srf05_1 > temp_srf05_1 then
               set_motor(speed, speed div 2)
          else
               if srf05_2 > temp_srf05_2 then
                    set_motor(speed div 2, speed)
               else
                    set_motor(0x00, 0x00)
               end if
          end if
     end if
          if ( temp_srf05_1<10) and (temp_srf05_2<10) then
             set_motor(0x00, 0x00)
          end if
end sub


sub procedure seguidor_linea(dim speed as byte)

     sensores = (linea3*8) + (linea2*4) + (linea1*2) + (linea0*1)

     select case sensores
     case 0' S4 S3 S2 S1 = 0 0 0 0
          banda = banda + 1
          if banda < 4 then
               set_motor(speed, speed)
               Delay_ms(100)
          else
               if (banda>4) and (banda<24) then
                    set_motor(-(speed div 2), (speed div 2))
                    Delay_ms(10)
               else
                    set_motor((speed div 2), -(speed div 2))
                    Delay_ms(10)
                    Delay_ms(10)
               end if
          end if
     case 1' S4 S3 S2 S1 = 0 0 0 1
          set_motor( (speed div 2), -(speed div 2))
          Delay_ms(10)
          banda = 0
     case 2' S4 S3 S2 S1 = 0 0 1 0
          set_motor( (speed div 2), (speed div 2))
          Delay_ms(10)
          banda = 0
     case 3' S4 S3 S2 S1 = 0 0 1 1      4
          set_motor( (speed div 2), -(speed div 2) )
          Delay_ms(10)
          Delay_ms(10)
          banda = 0
     case 4' S4 S3 S2 S1 = 0 1 0 0
          SET_MOTOR(-(speed div 2), (speed div 2))
          Delay_ms(10)
          banda = 0
     case 5' S4 S3 S2 S1 = 0 1 0 1
          set_motor((speed div 2), -(speed div 2))
          Delay_ms(10)
          banda = 0
     case 6' S4 S3 S2 S1 = 0 1 1 0    4
          set_motor(-(speed div 2), (speed div 2))
          Delay_ms(10)
          banda = 0
     case 7' S4 S3 S2 S1 = 0 1 1 1
          set_motor( (speed div 2), (speed div 2))
          Delay_ms(10)
          banda = 0
     case 8' S4 S3 S2 S1 = 1 0 0 0
          set_motor( speed, speed)
          Delay_ms(10)
          Delay_ms(10)
          banda = 0
     case 9' S4 S3 S2 S1 = 1 0 0 1    x
          set_motor( (speed div 2), -(speed div 2))
          Delay_ms(10)
          banda = 0
     case 10'S4 S3 S2 S1 = 1 0 1 0   x
          set_motor( speed , speed )
          Delay_ms(10)
          Delay_ms(10)
          banda=0
     case 11'S4 S3 S2 S1 = 1 0 1 1   x    4
          set_motor((speed div 2), -(speed div 2))
          Delay_ms(10)
          banda=0
     case 12'S4 S3 S2 S1 = 1 1 0 0    x
          set_motor(-(speed div 2), (speed div 2))
          Delay_ms(10)
          banda=0
     case 13'S4 S3 S2 S1 = 1 1 0 1    x
          set_motor((speed div 2), (speed div 2))
          Delay_ms(10)
          banda=0
     case 14'S4 S3 S2 S1 = 1 1 1 0    x  2
          set_motor(-(speed div 2), (speed div 2))
          Delay_ms(10)
          banda=0
     case 15'S4 S3 S2 S1 = 1 1 1 1    x
          set_motor( (speed div 2), (speed div 2))
          Delay_ms(10)
          banda=0
     end select
end sub

main:
     OSCCON = 0x65
     OPTION_REG = 0X86
     
     TRISA = 0x01
     PORTA = 0x00
     
     TRISB = 0x35
     PORTB = 0X00
     
     TRISC = 0xF0
     PORTC = 0X00
     
     TRISE = 0x08
     PORTE = 0x08
     
     ANSEL = 0X01
     ANSELH = 0X28
     
     Delay_ms(250)
     
     PWM1_Init(5000)
     PWM1_Set_Duty(0x00)
     PWM1_Start()

     PWM2_Init(5000)
     PWM2_Set_Duty(0x00)
     PWM2_Start()
     
     Delay_ms(250)
     
     temp_ldr1 = 0
     temp_ldr2 = 0
     banda = 0
     
     for i=0 to 63
          temp_ldr1 = temp_ldr1 + Adc_Read(13)
          Delay_ms(10)
          temp_ldr2 = temp_ldr2 + Adc_Read(11)
          Delay_ms(10)
     next i
     
     adc_ldr1 = temp_ldr1 div 64
     adc_ldr2 = temp_ldr2 div 64
     
     adc_ldr1 = adc_ldr1 + 0X0F
     adc_ldr2 = adc_ldr2 + 0X0F
     
     while 1
          velocidad = Adc_Read(0)>>2
          
          if button(PORTE, 3, 1, 1) then
               old_state = 0xFF
          end if
          
          if button(PORTE, 3, 1, 0) and old_state then
               old_state = 0x00
               inc(modo)
          end if
          
          select case modo
               case 1
                    PORTB.7 = 1
                    PORTB.6 = 0
                    seguidor_luz( velocidad )
                    Delay_ms(100)
               case 2
                    PORTB.7 = 0
                    PORTB.6 = 1
                    seguidor_linea( velocidad )
                    Delay_ms(100)
               case else
                    PORTB.7 = 1
                    PORTB.6 = 1
                    seguidor_ultrasonico( 30 , velocidad )
                    modo = 0
          end select
     wend
end.
